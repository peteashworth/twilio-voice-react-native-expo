buildscript {
  ext.versions = [
    'java'               : JavaVersion.VERSION_17,
    'androidGradlePlugin': '8.2.1',
    'googleServices'     : '4.4.0',
    'kotlinVersion'      : '1.9.22',
    'voiceAndroid'       : '6.7.1',
    'androidxCore'       : '1.12.0',
    'androidxLifecycle'  : '2.7.0',
    'audioSwitch'        : '1.2.2',
    'firebaseMessaging'  : '23.4.0'
  ]
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
      classpath "com.google.gms:google-services:${versions.googleServices}"
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlinVersion}"
    }
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  namespace 'com.twiliovoicereactnative'
  compileSdk safeExtGet('TwilioVoiceReactNative_compileSdkVersion', 34)
  defaultConfig {
    minSdkVersion safeExtGet('TwilioVoiceReactNative_minSdkVersion', 24)
    targetSdkVersion safeExtGet('TwilioVoiceReactNative_targetSdkVersion', 34)
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility versions.java
    targetCompatibility versions.java
  }
  buildFeatures {
    // for buildconfig construction
    buildConfig true
  }
}

repositories {
  google()
  mavenCentral()
  maven {
    url 'https://maven.google.com/'
    name 'Google'
  }
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
  implementation("com.twilio:voice-android:${versions.voiceAndroid}") {
    exclude group: 'com.android.support'
  }
  implementation "androidx.core:core:${versions.androidxCore}"
  implementation "androidx.lifecycle:lifecycle-runtime:${versions.androidxLifecycle}"
  implementation "androidx.lifecycle:lifecycle-viewmodel:${versions.androidxLifecycle}"
  implementation "androidx.lifecycle:lifecycle-livedata:${versions.androidxLifecycle}"
  implementation "com.google.firebase:firebase-messaging:${versions.firebaseMessaging}"
  implementation("com.twilio:audioswitch:${versions.audioSwitch}") {
    exclude group: 'com.android.support'
  }
  implementation 'com.google.android.material:material:1.1.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlinVersion}"

  // Expo modules dependencies
  //compileOnly 'expo.modules:expo-modules-core:+'

  constraints {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
      because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
      because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.0") {
      because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
    }
  }
}
